buildscript {
    ext.kotlin_version = '1.1.51'
    // See https://youtrack.jetbrains.com/issue/KT-21369
    // ext.kotlin_version = '1.1.+'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.8"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.6'
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:19.+'
    compile 'log4j:log4j:1.2.+'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'org.apache.commons:commons-math3:3.+'
    compile 'org.iq80.snappy:snappy:0.4'
    compile 'com.indeed:util-mmap:1.0.+'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.+'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}

dokka {
    linkMapping {
        dir = 'src/main/kotlin'
        url = "https://github.com/JetBrains-Research/big/blob/${project.version}/src/main/kotlin"
        suffix = '#L'
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    group = groupId
    version = project.version

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'big'
        userOrg = 'jetbrains-research'
        licenses = ['MIT']
        version {
            name = project.version.toString()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
